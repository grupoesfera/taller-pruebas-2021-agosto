plugins {
	id 'org.springframework.boot' version '2.5.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.unbroken-dome.test-sets' version '4.0.0'
	id "org.flywaydb.flyway" version '7.11.3'
}

group = 'ar.com.grupoesfera'
version = '1.0'
sourceCompatibility = '16'

repositories {
	mavenCentral()
}

testSets {
	integrationTest
	uiTest
	manualTest
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.2'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	integrationTestImplementation 'org.testcontainers:mariadb:1.15.2'
	integrationTestImplementation 'org.flywaydb:flyway-core:7.11.3'
	integrationTestImplementation 'io.rest-assured:rest-assured:4.4.0'
	integrationTestImplementation 'net.javacrumbs.json-unit:json-unit:2.27.0'

	uiTestImplementation 'org.seleniumhq.selenium:selenium-java:3.141.59'
	uiTestImplementation 'io.github.bonigarcia:webdrivermanager:4.4.3'

	manualTestImplementation 'org.flywaydb:flyway-core:7.11.3'
	manualTestRuntimeOnly 'org.testcontainers:mariadb:1.15.2'
}

test {
	useJUnitPlatform()
}

integrationTest {
	useJUnitPlatform()
	mustRunAfter test
}

uiTest() {
	useJUnitPlatform()
	mustRunAfter integrationTest
}

check {
	dependsOn integrationTest
	dependsOn uiTest
}

flyway {
	user = 'repartirUser'
	password = 'repartirPassword'
	url = 'jdbc:mariadb://localhost:3306/repartir'
	locations = [ 'filesystem:src/main/db/migrations' ]
}

bootRun {
	systemProperty "spring.profiles.active", "manualTest"
	classpath = sourceSets.manualTest.runtimeClasspath
}